#include<oit_node.glsl>

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

uniform sampler2DRect u_GBufferDepthMap;

layout (std430) buffer ssbo_OITNodesBuffer { uint OITMaxNumNodes; uint OITNumNodes; OITNode OITNodes[]; };
layout (r32ui) uniform uimage2DRect u_OITIndicesImage;
layout (r32f) uniform image2DRect u_OITDepthImage;

uint sortOITNodes(in uint startIndex)
{
    uint sortedIndex = 0xFFFFFFFF;

    uint currentIndex = startIndex;
    float currentDepth = 0.0f;
    
    while (currentIndex != 0xFFFFFFFF)
    {
        currentDepth = OITNodes[currentIndex].depth;
    
        uint nextIndex = OITNodes[currentIndex].next;
        
        if ((sortedIndex == 0xFFFFFFFF) || (OITNodes[sortedIndex].depth < currentDepth))
        {
            OITNodes[currentIndex].next = sortedIndex;
            sortedIndex = currentIndex;
        }
        else
        {
            uint newIndex = sortedIndex;
            while ((OITNodes[newIndex].next != 0xFFFFFFFF) && (OITNodes[OITNodes[newIndex].next].depth > currentDepth))
                newIndex = OITNodes[newIndex].next;

            OITNodes[currentIndex].next = OITNodes[newIndex].next;
            OITNodes[newIndex].next = currentIndex;
        }
        
        currentIndex = nextIndex;
    }
    
    return sortedIndex;
}

void main(void)
{
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    
    if (all(lessThan(texelCoord, imageSize(u_OITIndicesImage))))
    {
        uint OITIndex = imageLoad(u_OITIndicesImage, texelCoord).r;
        OITIndex = sortOITNodes(OITIndex);
        imageStore(u_OITIndicesImage, texelCoord, uvec4(OITIndex));
    }
    
    if (all(lessThan(texelCoord, imageSize(u_OITDepthImage))))
    {
        float depth = texelFetch(u_GBufferDepthMap, texelCoord).r;
        uint OITIndex = imageLoad(u_OITIndicesImage, texelCoord).r;
        while (OITIndex != 0xFFFFFFFF)
        {
            depth = OITNodes[OITIndex].depth;
            OITIndex = OITNodes[OITIndex].next;
        }
        imageStore(u_OITDepthImage, texelCoord, vec4(depth));
    }
}
